2.Mini C的语法

Mini C 的BNF语法如下:
1. program-> definition-list
2. definition-list-> definition-list definition | definition
3. definition-> variable-definition | function-definition
4. variable-definition-> type-indicator ID ；|type-indicator ID[NUM]；
5. type-indicator-> int | float | real| void
6. function-definition-> type-indicator ID（parameters）compound-stmt
7. parameters->parameter-list | void
8. parameter-list->parameter-list, parameter | parameter
9. parameter-> type-indicator ID | type-indicator ID[ ]
10.compound-stmt-> { local-definitions statement-list }
11.local-definitions-> local-definitions variable-definition |empty
12.statement-list-> statement-list statement | empty
13.statement->expression-stmt | compound-stmt | condition-stmt|while-stmt |for-stmt | return-stmt
14.expression-stmt-> expression ； | ；
15.condition-stmt-> if(expression) statement |if（expression）statement else statement
16.while-stmt-> while ( expression) statement
17.for-stmt-> for(for-init;expression;for-update) statement
18.for-init-> expression | variable-definition | empty
19.for-update-> expression-list | empty
20.expression-list-> expression-list, expression | expression
21.return-stmt->return ；| return expression ；
22.expression-> variable=expression | simple-expression
23.variable->ID |ID[expression]
24.simple-expression->additive-expression relop additive-expression
|additive-expression
25.relop-><=|<|>|>=|==|!=
26 additive-expression->additive-expression addop term | term
27.addop->+|-
28.term->term mulop factor | factor
29.mulop->*|/|% |^
30.factor->(expression )| variable | call | NUM | ++variable| --variable
31.call->ID(arguments)
32.arguments->argument-list | empty
33.argument-list->argument-list,expression | expression


注意：empty是代表空，输入时可以采用#来替代。